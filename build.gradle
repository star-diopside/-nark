buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE"
    }
}

allprojects {
    group = "jp.gr.java_conf.star_diopside.nark"
    version = "1.0.0-SNAPSHOT"

    ext {
        normalizeCssVersion = "3.0.2"
        bootstrapVersion = "3.3.7-1"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "eclipse-wtp"
    apply plugin: "checkstyle"
    apply plugin: "findbugs"
    apply plugin: "jacoco"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
    }

    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:Brussels-SR4"
        }
    }

    javadoc {
        options.charSet = "UTF-8"
        options.encoding = "UTF-8"
    }

    checkstyle {
        toolVersion = "7.1.1"
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures = true
    }

    findbugs {
        toolVersion = "3.0.1"
        ignoreFailures = true
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacoco {
        toolVersion = "0.7.7.201606060606"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = "javadoc"
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project(":nark-data") {
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "org.springframework.security:spring-security-core"
        compile "org.hibernate:hibernate-validator"
    }
}

project(":nark-service") {
    dependencies {
        compile project(":nark-data")
    }
}

project(":nark-controller") {
    dependencies {
        compile project(":nark-service")
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "org.springframework.boot:spring-boot-starter-security"
    }
}

project(":nark-view") {
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-thymeleaf"
        compile "org.thymeleaf.extras:thymeleaf-extras-springsecurity4"
        compile "org.webjars:normalize.css:${normalizeCssVersion}"
        compile "org.webjars:bootstrap:${bootstrapVersion}"
    }
}

project(":nark-web") {
    dependencies {
        compile project(":nark-controller")
        compile project(":nark-view")
        compile("org.springframework.session:spring-session") {
            exclude group: "commons-logging", module: "commons-logging"
        }
        compile "org.liquibase:liquibase-core"
    }
}

project(":nark-web-embed") {
    apply plugin: "org.springframework.boot"

    dependencies {
        compile project(":nark-web")
        runtime "org.postgresql:postgresql"
    }

    bootRepackage {
        mainClass = "jp.gr.java_conf.star_diopside.nark.Server"
    }
}

project(":nark-web-war") {
    apply plugin: "war"

    dependencies {
        compile project(":nark-web")
        providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"
        providedRuntime "org.postgresql:postgresql"
    }

    war {
        archiveName = "nark-web.war"
    }

    eclipse {
        wtp {
            facet {
                facet name: "jst.web", version: "3.1"
                facet name: "wst.jsdt.web", version: "1.0"
            }
        }
    }
}

project(":nark-user-registration") {
    apply plugin: "org.springframework.boot"

    dependencies {
        compile project(":nark-service")
        compile "commons-cli:commons-cli"
        runtime "org.apache.tomcat.embed:tomcat-embed-el"
        runtime "org.postgresql:postgresql"
    }
}
