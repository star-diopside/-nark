buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE"
    }
}

allprojects {
    group = "jp.gr.java_conf.star_diopside.nark"
    version = "1.0-SNAPSHOT"

    ext {
        lombokVersion = "1.16.10"

        springBootVersion = "1.4.0.RELEASE"
        commonsCliVersion = "1.3.1"
        postgresqlVersion = "9.4.1209"

        normalizeCssVersion = "3.0.2"
        bootstrapVersion = "3.3.7-1"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "checkstyle"
    apply plugin: "findbugs"
    apply plugin: "jacoco"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        jcenter()
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
        }
    }

    checkstyle {
        toolVersion "7.1.1"
        configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures true
    }

    findbugs {
        toolVersion = "3.0.1"
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacoco {
        toolVersion = "0.7.7.201606060606"
    }
}

project("nark-data") {
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "org.hibernate:hibernate-validator"
    }
}

project("nark-service") {
    dependencies {
        compile project(":nark-data")
        compile "org.springframework.security:spring-security-core"
    }
}

project("nark-controller") {
    dependencies {
        compile project(":nark-service")
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "org.springframework.boot:spring-boot-starter-security"
    }
}

project("nark-view") {
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-thymeleaf"
        compile "org.thymeleaf.extras:thymeleaf-extras-springsecurity4"
        compile "org.webjars:normalize.css:${normalizeCssVersion}"
        compile "org.webjars:bootstrap:${bootstrapVersion}"
    }
}

project("nark-web") {
    apply plugin: "spring-boot"

    dependencies {
        compile project(":nark-controller")
        compile project(":nark-view")
        compile "org.liquibase:liquibase-core"
        runtime "org.postgresql:postgresql:${postgresqlVersion}"
    }
}

project("nark-user-registration") {
    apply plugin: "spring-boot"

    dependencies {
        compile project(":nark-service")
        compile "commons-cli:commons-cli:${commonsCliVersion}"
        runtime "org.postgresql:postgresql:${postgresqlVersion}"
    }
}
